From 6ad04584150925c73dd834542571a725720b84c4 Mon Sep 17 00:00:00 2001
From: Phantom X <PhantomX@users.noreply.github.com>
Date: Sat, 1 Feb 2020 23:05:59 -0300
Subject: [PATCH] Use system libraries

FindCubeb.cmake, CheckLib.cmake and enet search from Dolphin-emu project:
https://github.com/dolphin-emu/dolphin
https://github.com/dolphin-emu/dolphin/raw/master/CMake/CheckLib.cmake
https://github.com/dolphin-emu/dolphin/raw/master/CMake/FindCubeb.cmake

FindZstd.cmake from Debian apt
https://salsa.debian.org/tbm/apt/raw/master/CMake/FindZstd.cmake
---
 CMakeLists.txt                          |  2 -
 externals/CMakeLists.txt                | 23 ++++++----
 externals/cmake-modules/CheckLib.cmake  | 57 +++++++++++++++++++++++++
 externals/cmake-modules/FindCubeb.cmake | 15 +++++++
 externals/cmake-modules/FindZstd.cmake  | 25 +++++++++++
 src/common/CMakeLists.txt               |  2 +-
 6 files changed, 113 insertions(+), 11 deletions(-)
 create mode 100644 externals/cmake-modules/CheckLib.cmake
 create mode 100644 externals/cmake-modules/FindCubeb.cmake
 create mode 100644 externals/cmake-modules/FindZstd.cmake

diff --git a/CMakeLists.txt b/CMakeLists.txt
index 41d55f3..470085d 100644
--- a/CMakeLists.txt
+++ b/CMakeLists.txt
@@ -2,7 +2,6 @@
 cmake_minimum_required(VERSION 3.8)
 list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/CMakeModules")
 list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/externals/cmake-modules")
-include(DownloadExternals)
 include(CMakeDependentOption)
 
 project(citra)
@@ -55,7 +54,6 @@ function(check_submodules_present)
         endif()
     endforeach()
 endfunction()
-check_submodules_present()
 
 configure_file(${PROJECT_SOURCE_DIR}/dist/compatibility_list/compatibility_list.qrc
                ${PROJECT_BINARY_DIR}/dist/compatibility_list/compatibility_list.qrc
diff --git a/externals/CMakeLists.txt b/externals/CMakeLists.txt
index 63159a5..41a3a4f 100644
--- a/externals/CMakeLists.txt
+++ b/externals/CMakeLists.txt
@@ -1,7 +1,7 @@
 # Definitions for all external bundled libraries
 
 set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} ${PROJECT_SOURCE_DIR}/CMakeModules)
-include(DownloadExternals)
+include(CheckLib)
 include(ExternalProject)
 
 # Catch
@@ -63,17 +63,24 @@ if (ARCHITECTURE_x86_64)
 endif()
 
 # Zstandard
-add_subdirectory(zstd/build/cmake EXCLUDE_FROM_ALL)
-target_include_directories(libzstd_static INTERFACE ./zstd/lib)
+find_package(Zstd)
 
 # ENet
-add_subdirectory(enet)
-target_include_directories(enet INTERFACE ./enet/include)
+check_lib(ENET libenet enet enet/enet.h QUIET)
+include(CheckSymbolExists)
+if (ENET_FOUND)
+  set(CMAKE_REQUIRED_INCLUDES ${ENET_INCLUDE_DIRS})
+  set(CMAKE_REQUIRED_FLAGS ${ENET_LDFLAGS})
+  set(CMAKE_REQUIRED_LIBRARIES ${ENET_LIBRARIES})
+  check_symbol_exists(enet_socket_get_address enet/enet.h ENET_HAVE_SGA)
+  set(CMAKE_REQUIRED_INCLUDES)
+  set(CMAKE_REQUIRED_FLAGS)
+  set(CMAKE_REQUIRED_LIBRARIES)
+endif()
 
 # Cubeb
 if (ENABLE_CUBEB)
-    set(BUILD_TESTS OFF CACHE BOOL "")
-    add_subdirectory(cubeb EXCLUDE_FROM_ALL)
+    find_package(Cubeb)
 endif()
 
 # DiscordRPC
@@ -106,4 +113,4 @@ if (ENABLE_WEB_SERVICE)
 endif()
 
 # lodepng
-add_subdirectory(lodepng)
\ No newline at end of file
+add_subdirectory(lodepng)
diff --git a/externals/cmake-modules/CheckLib.cmake b/externals/cmake-modules/CheckLib.cmake
new file mode 100644
index 0000000..e6408a4
--- /dev/null
+++ b/externals/cmake-modules/CheckLib.cmake
@@ -0,0 +1,57 @@
+find_package(PkgConfig)
+
+macro(_internal_message msg)
+	if(NOT ${_is_quiet})
+		message(STATUS "${msg}")
+	endif()
+endmacro()
+
+macro(check_lib var pc lib)
+	set(_is_required 0)
+	set(_is_quiet 0)
+	set(_arg_list ${ARGN})
+	foreach(_arg ${ARGN})
+		if(_arg STREQUAL "REQUIRED")
+			list(REMOVE_ITEM _arg_list "REQUIRED")
+			set(_is_required 1)
+		endif()
+		if(_arg STREQUAL "QUIET")
+			list(REMOVE_ITEM _arg_list "QUIET")
+			set(_is_quiet 1)
+		endif()
+	endforeach()
+
+	if(PKG_CONFIG_FOUND AND NOT ${var}_FOUND)
+		pkg_search_module(${var} QUIET ${pc})
+	endif()
+
+	if(${var}_FOUND)
+		if(${var}_INCLUDE_DIRS)
+			include_directories(${${var}_INCLUDE_DIRS})
+		endif()
+		# Make sure include directories for headers found using find_path below
+		# are re-added when reconfiguring
+		if(${var}_INCLUDE)
+			include_directories(${${var}_INCLUDE})
+		endif()
+		_internal_message("${lib} found")
+	else()
+		find_library(${var} ${lib})
+		if(_arg_list)
+			find_path(${var}_INCLUDE ${_arg_list})
+		else()
+			set(${var}_INCLUDE FALSE)
+		endif()
+		if(${var} AND ${var}_INCLUDE)
+			include_directories(${${var}_INCLUDE})
+			_internal_message("${lib} found")
+			set(${var}_FOUND 1 CACHE INTERNAL "")
+		else()
+			if(_is_required)
+				message(FATAL_ERROR "${lib} is required but not found")
+			else()
+				_internal_message("${lib} not found")
+			endif()
+		endif()
+	endif()
+endmacro()
diff --git a/externals/cmake-modules/FindCubeb.cmake b/externals/cmake-modules/FindCubeb.cmake
new file mode 100644
index 0000000..c0a730c
--- /dev/null
+++ b/externals/cmake-modules/FindCubeb.cmake
@@ -0,0 +1,15 @@
+find_path(CUBEB_INCLUDE_DIR cubeb.h PATH_SUFFIXES cubeb)
+find_library(CUBEB_LIBRARY cubeb)
+mark_as_advanced(CUBEB_INCLUDE_DIR CUBEB_LIBRARY)
+
+include(FindPackageHandleStandardArgs)
+find_package_handle_standard_args(CUBEB DEFAULT_MSG
+	CUBEB_INCLUDE_DIR CUBEB_LIBRARY)
+
+if(CUBEB_FOUND AND NOT TARGET CUBEB)
+  add_library(cubeb::cubeb UNKNOWN IMPORTED)
+  set_target_properties(cubeb::cubeb PROPERTIES
+    IMPORTED_LOCATION "${CUBEB_LIBRARY}"
+    INTERFACE_INCLUDE_DIRECTORIES "${CUBEB_INCLUDE_DIR}"
+  )
+endif()
diff --git a/externals/cmake-modules/FindZstd.cmake b/externals/cmake-modules/FindZstd.cmake
new file mode 100644
index 0000000..6811804
--- /dev/null
+++ b/externals/cmake-modules/FindZstd.cmake
@@ -0,0 +1,25 @@
+# - Try to find ZSTD
+# Once done, this will define
+#
+#  ZSTD_FOUND - system has ZSTD
+#  ZSTD_INCLUDE_DIRS - the ZSTD include directories
+#  ZSTD_LIBRARIES - the ZSTD library
+find_package(PkgConfig)
+
+pkg_check_modules(ZSTD_PKGCONF libzstd)
+
+find_path(ZSTD_INCLUDE_DIRS
+  NAMES zstd.h
+  PATHS ${ZSTD_PKGCONF_INCLUDE_DIRS}
+)
+
+
+find_library(ZSTD_LIBRARIES
+  NAMES zstd
+  PATHS ${ZSTD_PKGCONF_LIBRARY_DIRS}
+)
+
+include(FindPackageHandleStandardArgs)
+find_package_handle_standard_args(ZSTD DEFAULT_MSG ZSTD_INCLUDE_DIRS ZSTD_LIBRARIES)
+
+mark_as_advanced(ZSTD_INCLUDE_DIRS ZSTD_LIBRARIES)
diff --git a/src/common/CMakeLists.txt b/src/common/CMakeLists.txt
index af07ac2..4e63d26 100644
--- a/src/common/CMakeLists.txt
+++ b/src/common/CMakeLists.txt
@@ -122,7 +122,7 @@ endif()
 create_target_directory_groups(common)
 
 target_link_libraries(common PUBLIC fmt microprofile)
-target_link_libraries(common PRIVATE libzstd_static)
+target_link_libraries(common PRIVATE zstd)
 if (ARCHITECTURE_x86_64)
     target_link_libraries(common PRIVATE xbyak)
 endif()
-- 
2.24.1

